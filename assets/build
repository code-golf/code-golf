#!/usr/bin/perl

use v5.26;
use warnings;

use Digest::MD5 'md5_base64';
use IPC::Open2 'open2';

system qw(svgo -q -f assets/includes);

open my $fh, '>', 'routes/assets.go';

print $fh <<GO;
package routes

import (
	"net/http"
	"strings"
)

func Asset(w http.ResponseWriter, r *http.Request) {
	switch r.URL.Path {
GO

my ( %data, %paths );

for (
    sort { $b =~ /\.woff2$/ <=> $a =~ /\.woff2$/ or $a cmp $b }
    <assets/*.{css,js,png,svg,woff2}>
) {
    say;

    my $data = slurp($_);

    s|^assets/||;

    my $name = s/\.(\w)/\U$1/r;

    if ( $_ eq 'favicon.svg' ) {
        $data{$name} = $data;

        print $fh <<'GO';
	case "/favicon.svg":
		w.Header().Set("Content-Type", "image/svg+xml")
		w.Write(faviconSvg)
GO

        next;
    }
    if ( $_ eq 'favicon16.png' ) {
        $data{$name} = $data;

        print $fh <<'GO';
	case "/favicon16.png":
		w.Header().Set("Content-Type", "image/png")
		w.Write(favicon16Png)
GO

        next;
    }
    if ( $_ eq 'favicon32.png' ) {
        $data{$name} = $data;

        print $fh <<'GO';
	case "/favicon32.png":
		w.Header().Set("Content-Type", "image/png")
		w.Write(favicon32Png)
GO

        next;
    }
    elsif (/\.woff2$/) {
        $data{$name} = $data;
        my $path = $paths{$name} = '/assets/' . md5_base64($data) =~ y|+/|-_|r;

        print $fh <<GO;
	case "$path":
		w.Header().Set("Cache-Control", "max-age=9999999,public")
		w.Header().Set("Content-Type", "font/woff2")
		w.Write($name)
GO

        next;
    }

    $data =~ s|/\* include ([^ ]+) +\*/|slurp("assets/includes/$1")|eg;

    if (/\.css$/) {
        $data =~ s|svg\(([^#]+)(.+)\)|svg($1, $2)|eg;
        $data =~ s|fontWoff2|$paths{fontWoff2}|;
        $data =~ s|twemojiWoff2|$paths{twemojiWoff2}|;

        $data = run($data, 'csso');
    }
    else {
        my @cmd = qw(java -jar /closure-compiler.jar --language_out ES_2017);

        $data = run( $data, @cmd );
    }

    $data{"${name}Br"} = run($data, 'brotli');

    # Zopfli can't stream https://github.com/google/zopfli/issues/17
    open my $tmp, '>', '/tmp/zopfli';
    print $tmp $data;
    close $tmp;

    $data{"${name}Gz"} = `zopfli -c /tmp/zopfli`;

    my $mime = /\.css$/ ? 'text/css' : 'application/javascript';
    my $path = $paths{$name} = '/assets/' . md5_base64($data) =~ y|+/|-_|r;

    print $fh <<GO;
	case ${name}Path:
		w.Header().Set("Cache-Control", "max-age=9999999,public")
		w.Header().Set("Content-Type", "$mime")

		if strings.Contains(r.Header.Get("Accept-Encoding"), "br") {
			w.Header().Set("Content-Encoding", "br")
			w.Write(${name}Br)
		} else {
			w.Header().Set("Content-Encoding", "gzip")
			w.Write(${name}Gz)
		}
GO
}

print $fh "\t}\n}\n\n";
print $fh qq/const ${_}Path = "$paths{$_}"\n/ for sort keys %paths;
print $fh "\n";

for ( sort keys %data ) {
    printf "%12s = %5d bytes\n", $_, length $data{$_};

    my $i = 0;
    my $data = join ',',
        map +( "\n" x !($i++ % 15) ) . "0x$_", unpack '(H2)*', $data{$_};

    print $fh "var $_ = []byte{$data}\n\n";
}

sub run {
    my ( $data, @cmd ) = @_;

    local $/;

    open2( my $out, my $in, @cmd );
    print $in $data;
    close $in;

    scalar <$out>;
}

sub slurp { local ( @ARGV, $/ ) = @_; scalar <> }

sub svg {
    my ( $file, $fill ) = @_;

    local $_ = slurp("assets/includes/$file.svg");

    s/FILL/$fill/g;
    s/#/%23/g;
    s/"/'/g;

    qq(url("data:image/svg+xml,$_"));
}
