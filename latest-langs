#!/usr/bin/env perl6

use Cro::HTTP::Client;

constant @langs = (
    'C',
    'http://download.savannah.gnu.org/releases/tinycc/?C=M&O=D',
    / 'tcc-' (<[\d.]>+) '.tar.bz2' /,

    'Fortran',
    'https://gcc.gnu.org/releases.html',
    / 'GCC ' (\d<[\d.]>+) /,

    'Go',
    'https://golang.org/project/',
    / 'var goVersion = "go' (<[\d.]>+) '"' /,

    'Haskell',
    'https://www.haskell.org/ghc/download.html',
    / 'download_ghc' .+? ">" (<[\d.]>+) /,

    'Java',
    # Note: need to manually change the number in this url for major releases.
    'https://raw.githubusercontent.com/AdoptOpenJDK/openjdk-docker/master/14/jdk/alpine/Dockerfile.hotspot.releases.slim',
    / 'ENV JAVA_VERSION jdk-' (<[\d+.]>+) /,

    'Julia',
    'https://julialang.org/downloads/',
    / 'stable release: v' (<[\d.]>+) /,

    'Lua',
    'https://www.lua.org',
    / 'versions.html' .+? '>Lua ' (<[\d.]>+) /,

    'Nim',
    'https://nim-lang.org',
    / 'Version ' (<[\d.]>+) /,

    'Perl',
    'https://www.perl.org',
    / '"version-highlight">' (<[\d.]>+) /,

    'PHP',
    'https://www.php.net/downloads.php',
    / 'Stable</span>' \s* 'PHP ' (<[\d.]>+) /,

    'Python',
    'https://www.python.org',
    / 'Latest: <' .+? '>Python ' (<[\d.]>+) /,

    'Ruby',
    'https://www.ruby-lang.org/en/downloads/',
    / 'stable version is ' (<[\d.]>+) '.' /,

    'Rust',
    'https://www.rust-lang.org',
    / 'Version ' (<[\d.]>+) /,

    'Swift',
    'https://swift.org/download/',
    / 'Swift ' (<[\d.]>+) /,
);

constant $len = @langs[0, 3 … *]».chars.max;

my %current = (get('https://code-golf.io/about') ~~ m:g/
    '<tr><th' .+? '>' (.+?) '<' .*? (<[\d+.]> ** 2..*)
/)».list.flat».Str.Hash;

for @langs -> $lang, $url, $re {
    my $ver = get($url) ~~ $re ?? ~$0 !! '';
    my $current = %current{$lang};
    if $ver ne $current && "$ver.0" ne $current {
        printf " %*s %s\n", $len, $lang, $ver;
    }
}

sub get ($url) {
    state $client = Cro::HTTP::Client.new(:!follow :http<1.1>);

    await (await $client.get($url)).body;
}
