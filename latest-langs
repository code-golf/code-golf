#!/usr/bin/env perl6

use Cro::HTTP::Client;
use TOML::Thumb;

constant @urls = (
    'Bash'       => 'Bash_(Unix_shell)',
    'C'          => 'Tiny_C_Compiler',
    'C#'         => 'C_Sharp_(programming_language)',
    'COBOL'      => 'GnuCOBOL',
    'F#'         => 'F_Sharp_(programming_language)',
    'Fortran'    => 'GNU_Fortran',
    'Go'         => 'Go_(programming_language)',
    'Haskell'    => 'Glasgow_Haskell_Compiler',
    'Java'       => 'Java_Development_Kit',
    'JavaScript' => 'V8_(JavaScript_engine)',
    'Julia'      => 'Julia_(programming_language)',
    'Lisp'       => 'CLISP',
    'Lua'        => 'Lua_(programming_language)',
    'Nim'        => 'Nim_(programming_language)',
    'PHP'        => 'PHP',
    'Perl'       => 'Perl',
    'PowerShell' => 'PowerShell',
    'Python'     => 'Python_(programming_language)',
    'Raku'       => 'Rakudo',
    'Ruby'       => 'Ruby_(programming_language)',
    'Rust'       => 'Rust_(programming_language)',
    'SQL'        => 'SQLite',
    'Swift'      => 'Swift_(programming_language)',
);

my %langs = from-toml slurp 'langs.toml';

for @urls -> (:key($lang), :value($path)) {
    state $client = Cro::HTTP::Client.new: :http<1.1>;

    my $old = %langs{$lang}<version> ~~ / <[\d.]>+ \d+ /;
    my $new = (await (await $client.get: "https://en.wikipedia.org/wiki/$path").body)
        ~~ / 'Stable release' .+? '>' ( '#' \d+ ' "' )? <(<[\d.]>+)> /;

    printf " %*s %s → %s\n", INIT { @urls».key».chars.max }, $lang, $old, $new
        unless $old eq $new or $old.starts-with("$new.");
}
