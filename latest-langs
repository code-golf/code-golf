#!/usr/bin/env raku

use HTTP::Tiny;
use JSON::Fast;
use TOML::Thumb;

# Pass "--all" to see all languages regardless of version status.
unit sub MAIN(Bool :$all, *@langs);

sub get {
    my $ua = HTTP::Tiny.new :throw-exceptions;
    return $ua.get("https://$^url")<content>.decode;
}

constant %paths = (
    '05AB1E'       => 'github.com/Adriandmen/05AB1E',
    '><>'          => 'github.com/primo-ppcg/fish-jit',
    'ALGOL 68'     => 'jmvdveer.home.xs4all.nl/en.download.algol-68-genie-current.html',
    'APL'          => 'dyalog.com/download-zone.htm?p=download',
    'Arturo'       => 'github.com/arturo-lang/arturo',
    'Assembly'     => 'registry.npmjs.org/@defasm/core/latest',
    'AWK'          => 'ftp.gnu.org/gnu/gawk/?C=M;O=D',
    'Bash'         => 'en.wikipedia.org/wiki/Bash_(Unix_shell)',
    'BASIC'        => 'en.wikipedia.org/wiki/FreeBASIC',
    'Befunge'      => 'github.com/VorpalBlade/cfunge',
    'Berry'        => 'github.com/berry-lang/berry/releases/latest',
    'BQN'          => 'aplwiki.com/wiki/BQN',
    'brainfuck'    => 'github.com/primo-ppcg/bfci',
    'C'            => 'en.wikipedia.org/wiki/Tiny_C_Compiler',
    'C#'           => 'en.wikipedia.org/wiki/C_Sharp_(programming_language)',
    'C++'          => 'github.com/llvm/llvm-project/releases/latest',
    'Civet'        => 'registry.npmjs.org/@danielx/civet/latest',
    'CJam'         => 'sourceforge.net/projects/cjam/files',
    'Clojure'      => 'github.com/babashka/babashka/releases/latest',
    'COBOL'        => 'en.wikipedia.org/wiki/GnuCOBOL',
    'Coconut'      => 'pypi.org/pypi/coconut/json',
    'CoffeeScript' => 'registry.npmjs.org/coffeescript/latest',
    'Common Lisp'  => 'en.wikipedia.org/wiki/CLISP',
    'Crystal'      => 'github.com/crystal-lang/crystal/releases/latest',
    'D'            => 'en.wikipedia.org/wiki/D_(programming_language)',
    'Dart'         => 'en.wikipedia.org/wiki/Dart_(programming_language)',
    'Egel'         => 'github.com/egel-lang/egel/releases/latest',
    'Elixir'       => 'github.com/elixir-lang/elixir/releases/latest',
    'Erlang'       => 'api.github.com/repos/erlang/otp/releases',
    'F#'           => 'en.wikipedia.org/wiki/F_Sharp_(programming_language)',
    'Factor'       => 'github.com/factor/factor/releases/latest',
    'Fennel'       => 'fennel-lang.org/downloads/?C=M;O=D',
    'Forth'        => 'ftp.gnu.org/gnu/gforth/?C=M;O=D',
    'Fortran'      => 'en.wikipedia.org/wiki/GNU_Compiler_Collection',
    'Gleam'        => 'github.com/gleam-lang/gleam/releases/latest',
    'Go'           => 'endoflife.date/api/go.json',
    'GolfScript'   => 'github.com/lynn/golfscript/tree/code-golf',
    'Groovy'       => 'endoflife.date/api/apache-groovy.json',
    'Harbour'      => 'sourceforge.net/projects/harbour-project/files',
    'Hare'         => 'lists.sr.ht/~sircmpwn/hare-announce?search=release',
    'Haskell'      => 'pkgs.alpinelinux.org/package/edge/community/x86_64/ghc',
    'Haxe'         => 'github.com/HaxeFoundation/haxe/releases/latest',
    'Hexagony'     => 'github.com/SirBogman/Hexagony',
    'Hush'         => 'crates.io/api/v1/crates/hush',
    'Hy'           => 'pypi.org/pypi/hy/json',
    'iogii'        => 'golfscript.com/iogii/source.html',
    'J'            => 'aplwiki.com/wiki/J',
    'Janet'        => 'github.com/janet-lang/janet/releases/latest',
    'Java'         => 'endoflife.date/api/java.json',
    'JavaScript'   => 'chromium.googlesource.com/v8/v8.git/+/' ~ get(
        'chromiumdash.appspot.com/fetch_releases?platform=linux',
    ).&from-json.first(*<channel> eq 'Stable')<hashes><v8>,
    'jq'           => 'github.com/jqlang/jq/releases/latest',
    'Julia'        => 'api.github.com/repos/JuliaLang/julia/releases',
    'K'            => 'codeberg.org/ngn/k',
    'Kotlin'       => 'github.com/JetBrains/kotlin/releases/latest',
    'Lua'          => 'en.wikipedia.org/wiki/Lua_(programming_language)',
    'Nim'          => 'en.wikipedia.org/wiki/Nim_(programming_language)',
    'OCaml'        => 'en.wikipedia.org/wiki/OCaml',
    'Odin'         => 'api.github.com/repos/odin-lang/Odin/releases',
    'Pascal'       => 'en.wikipedia.org/wiki/Free_Pascal',
    'Perl'         => 'endoflife.date/api/perl.json',
    'PHP'          => 'endoflife.date/api/php.json',
    'Picat'        => 'picat-lang.org/download.html',
    'PowerShell'   => 'endoflife.date/api/powershell.json',
    'Prolog'       => 'swi-prolog.discourse.group/c/releases',
    'Python'       => 'endoflife.date/api/python.json',
    'Qore'         => 'github.com/qorelanguage/qore/releases/latest',
    'R'            => 'en.wikipedia.org/wiki/R_(programming_language)',
    'Racket'       => 'github.com/racket/racket/releases/latest',
    'Raku'         => 'github.com/rakudo/rakudo/releases/latest',
    'Rebol'        => 'en.wikipedia.org/wiki/Rebol',
    'Rexx'         => 'regina-rexx.sourceforge.io',
    'Rockstar'     => 'github.com/RockstarLang/rockstar/releases/latest',
    'Ruby'         => 'endoflife.date/api/ruby.json',
    'Rust'         => 'github.com/rust-lang/rust/releases/latest',
    'Scala'        => 'github.com/scala/scala/releases/latest',
    'Scheme'       => 'en.wikipedia.org/wiki/Chez_Scheme',
    'sed'          => 'ftp.gnu.org/gnu/sed/?C=M;O=D',
    'SQL'          => 'en.wikipedia.org/wiki/SQLite',
    'Squirrel'     => 'github.com/albertodemichelis/squirrel/releases/latest',
    'Stax'         => 'github.com/tomtheisen/stax/releases/latest',
    'Swift'        => 'github.com/swiftlang/swift/releases/latest',
    'Tcl'          => 'en.wikipedia.org/wiki/Tcl',
    'TeX'          => 'en.wikipedia.org/wiki/TeX',
    'Uiua'         => 'crates.io/api/v1/crates/uiua',
    'V'            => 'en.wikipedia.org/wiki/V_(programming_language)',
    'VimL'         => 'en.wikipedia.org/wiki/Vim_(text_editor)',
    'Vyxal'        => 'github.com/Vyxal/Vyxal/releases/latest',
    'Wren'         => 'github.com/wren-lang/wren/releases/latest',
    'Zig'          => 'github.com/ziglang/zig/releases/latest',
);

my %langs = from-toml 'config/data/langs.toml'.IO;

# Dotted decimal, hyphenated date, or Git SHA.
my regex ver { \d+ ( <[.-]> \d+ )+ | <xdigit> ** 7..* }

for %langs{ @langs || * }:p.sort: *.key.fc -> (:key($name), :value(%lang)) {
    my $old = %lang<version> ~~ / <ver> /;
    my $new = do with %paths{$name} {
        my $res = get($_);

        when / 'alpinelinux'    / { $res ~~ / 'Version' .+? <(<ver>)> '-r' \d+ / }
        when / 'aplwiki'        / { $res ~~ / ' release' .+? <(<ver>)> / }
        when / 'chromium'       / { $res ~~ / 'Version' .+? <(<ver>)> / }
        when / 'codeberg'       / { $res ~~ / '"shortsha">' <(<ver>)> / }
        when / 'crates.io'      / { $res.&from-json<crate><max_stable_version> }
        when / 'dyalog.com'     / { $res ~~ / <(<ver>)> ' for Linux (DEB)' / }
        when / 'endoflife'      / { $res.&from-json[0]<latest> }
        when / 'fennel-lang'    / { $res ~~ / 'fennel-' <(<ver>)> / }
        when / 'ftp.gnu.org'    / { $res ~~ / '-' <(<ver>)> '.tar.gz' / }
        when / 'golfscript.com' / { $res ~~ / '(Alpha)' .+? <(<ver>)> / }
        when / 'jmvdveer'       / { $res ~~ / 'algol68g-' <(<ver>)> / }
        when / 'lists.sr.ht'    / { $res ~~ / 'Hare ' <(<ver>)> ' release' / }
        when / 'npmjs.org'      / { $res.&from-json[0]<version> }
        when / 'picat-lang.org' / { $res ~~ / 'Version ' <(<ver>)> / }
        when / 'pypi.org'       / { $res.&from-json<info><version> }
        when / 'regina-rexx'    / { $res ~~ / 'Current' .+? <(<ver>)> / }
        when / 'sourceforge'    / { $res ~~ / 'Latest' .+? <(<ver>)> / }
        when / 'swi-prolog'     / { $res ~~ / 'stable' .+? <(<ver>)> / }
        when / 'api.github.com' / {
            $res.&from-json.grep(!*<prerelease>).map({ .<name> ~~ / <ver> / })
                .sort({ Version.new: $_ }).tail;
        }
        when / 'github.com'     / {
            .ends-with('/releases/latest')
                ?? $res ~~ / \d+ ( '.' \d+ )+ /
                !! $res ~~ / '"currentOid":"' <( <xdigit> ** 7 )> /;
        }
        when / 'wikipedia'      / {
            .contains(any <C_Sharp TeX>)
                ?? $res ~~ / < current stable > ' version' .+? <(<ver>)> /
                !! $res ~~ /
                    < Stable Preview > ' release' .+? '>' ( '#' \d+ ' "' )?
                    <(<ver>)>
                /;
        }
    } // '?';

    my $match = $old eq $new || $old.starts-with("$new.") || "$old.0" eq $new;
    my $code  = $new eq '?' ?? 33 !! $match ?? 32 !! 31;

    printf "%12s \e[%dm%11s â†’ %s\e[0m\n", $name, $code, $old, $new
        if $all || @langs.elems || !$match;
}
